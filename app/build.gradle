apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.dsj.iptmethod"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 5
        versionName "ime_release_1.5"
    }

    signingConfigs {
        def ksFile = rootProject.file('keystore/keystore.properties')
        def props = new Properties();
        if (ksFile.canRead()) {
            props.load(new FileInputStream(ksFile))
        }

        release{
            storeFile file(props['PLATFORM_KEYSTORE_FILE'])
            storePassword props['PLATFORM_KEYSTORE_PASSWORD']
            keyAlias props['PLATFORM_KEYSTORE_ALIAS']
            keyPassword props['PLATFORM_KEYSTORE_ALIAS_PASSWORD']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        debug {
            debuggable true
        }
    }

    lintOptions {
        abortOnError false
    }

    // rename the apk with the version name
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            //get versionCode and set to manifest file
            output.versionCodeOverride = variant.versionCode

            //assemble versionName and set to manifest file
            def verFullName = variant.versionName
            output.versionNameOverride = verFullName

            //apk文件名称
            def apkName = verFullName + ".apk"     //应用名称
            apkName = apkName.toLowerCase()
            println("==>build apk: " + apkName)

            outputFileName = apkName
        }
    }

    externalNativeBuild {
        ndkBuild {
            path "src/main/jni/Android.mk"
        }
    }

    aaptOptions {
        noCompress "dat"
    }

    dependencies {
        implementation fileTree(dir: 'libs', include: ['*.jar'])
        implementation 'com.android.support:appcompat-v7:28.0.0'
        testImplementation 'junit:junit:4.12'
    }
}
